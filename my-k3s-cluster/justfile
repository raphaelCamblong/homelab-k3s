
kube_vip_endpoint := "192.168.1.144"
interface := "enX0"
cluster_name := "k3s_galideo"
user := "raphael"

master_0_ip:= "192.168.1.37"
master_1_ip:= "192.168.1.38"
master_2_ip:= "192.168.1.39"

worker_0_ip:= "192.168.1.40"
worker_1_ip:= "192.168.1.41"

# DEPLOYMENT PART
deploy:
    just deploy_master_init {{master_0_ip}}
    just deploy_master_init_check
    just deploy_master_init_vip

deploy_next:
    just deploy_master_join {{master_1_ip}}
    just deploy_master_join {{master_2_ip}}
    just deploy_agent_join {{worker_0_ip}}
    just deploy_agent_join {{worker_1_ip}}

deploy_master_init ip:
    # install k3s
    sudo k3sup install \
    --ip={{ip}} \
    --user=raphael \
    --ssh-key ~/.ssh/id_ed25519 \
    --sudo \
    --cluster \
    --tls-san={{kube_vip_endpoint}} \
    --k3s-channel=stable \
    --k3s-extra-args "--disable=traefik --disable=servicelb --flannel-iface={{interface}} --node-ip={{ip}}" \
    --local-path $HOME/.kube/config \
    --merge \
    --print-command \
    --context={{cluster_name}}
    just deploy_master_init_check

deploy_master_init_check:
    # Seting context
    kubectx {{cluster_name}}
    kubectl get no
    kubectl get po -A -o wide

deploy_master_init_vip:
    # install kube-vip
    kubectl apply -f https://kube-vip.io/manifests/rbac.yaml
    scp ./utils/install_kubeVIP.sh {{user}}@{{master_0_ip}}/
    ssh {{user}}@{{master_0_ip}}/ /path/to/directory/install_kube-vip.sh

deploy_master_join ip:
    k3sup join \
    --ip {{ip}} \
    --user=raphael \
    --ssh-key ~/.ssh/id_ed25519 \
    --sudo \
    --k3s-channel stable \
    --server \
    --server-ip {{kube_vip_endpoint}} \
    --server-user raphael

deploy_agent_join ip:
    k3sup join \
    --ip {{ip}} \
    --user=raphael \
    --ssh-key ~/.ssh/id_ed25519 \
    --sudo \
    --k3s-channel stable \
    --server-ip {{kube_vip_endpoint}} \
    --server-user raphael

# POPULATE PART
install_all_helm_chart:
  helm repo add longhorn https://charts.longhorn.io
  helm repo add prometheus-community https://prometheus-community.github.io/helm-charts
  helm repo add grafana https://grafana.github.io/helm-charts
  helm repo update

deploy_metalLB:
    kubectl apply -f https://raw.githubusercontent.com/metallb/metallb/v0.12.1/manifests/namespace.yaml
    kubectl apply -f ./metalLb/configMap.yaml
    kubectl apply -f https://raw.githubusercontent.com/metallb/metallb/v0.12.1/manifests/metallb.yaml

deploy_monitoring:
    kubectl create namespace monitoring
    helm install prometheus prometheus-community/prometheus -n monitoring --create-namespace
    helm install grafana grafana/grafana -f ./grafana/values.yaml -n monitoring

deploy_longhorn:
  helm install longhorn longhorn/longhorn --namespace longhorn-system -f ./longhorn/values.yml --create-namespace

# Test App
deploy_test_app:
    kubectl apply -f ./example/deployment.yml
    kubectl apply -f ./example/service.yml

delete_test_app:
    kubectl delete -f ./example/deployment.yml
    kubectl delete -f ./example/service.yml
    kubectl get deployments
    kubectl get svc

# Utils
get_kubectl_config ip:
    scp {{user}}@{{ip}}:/etc/rancher/k3s/k3s.yaml ./

uninstall:
    sudo /usr/local/bin/k3s-uninstall.sh

# BACKUP PART
backup_etcd:
    ssh {{user}}@{{master_0_ip}} sudo k3s etcd-snapshot save

